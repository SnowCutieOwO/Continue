# 区域操作

在你成功创建了一块选区之后，我们就要来上点猛料了。

[[toc]]

## 设置方块

最基本的操作，使用命令 `//set <方块>` 即可将选区内以指定内容全部填充。

::: info 示例：设置你的选区
简单图案：
```
//set stone
```
复杂图案（有关内容请参阅“图案”章节）：[^1]
```
//set 30%red_wool,*oak_log,#copy,##slabs
```
:::

## 替换方块

若你不想要一次修改*整片*选区的方块，你可以通过指定命令 `//replace [蒙版] <方块>` 中的蒙版参数来决定哪些方块应该修改。

需要注意的是你可以忽略这个蒙版参数——它在默认情况下为已存在的方块。

::: info 示例：替换选区内的方块 
将所有非空气方块替换为草：
```
//replace grass_block
```
将所有石头替换为绿色羊毛：
```
//replace stone green_wool
```
将地面上所有的沙子[^2]替换为矿物方块：
```
//replace "sand <air" diamond_block,iron_block,gold_block
```
:::

## 筑墙（及其他边界）

有些时候，你可能需要造一堵墙或者其他的中空图形。有两种方法做到：

对于长方体选区，你可以使用命令 `//faces <图案>` 命令（或：`//outline`）在选区边界的六个面上造墙（东南西北和上下方向）。在圈定其他种类选区的情况下使用该命令，则墙壁的放置区域会自动选择为边界框定区（该选区所能容纳的最大立方体的位置）。

对任意种类的选区，命令 `//walls [图案]` 能够帮你你在选区边界处的位置筑墙，但不包括天花板和地板。所筑墙壁的类型取决于选区种类。

## 覆盖

命令 `//overlay <图案>` 允许你在选区内已有的方块上覆盖另一层方块（或指定图案）。需要注意的是，你的选区顶部必须是“露天”的——这个命令将会按列向下匹配直至方块不为空气，然后再在检测到不为空气的方块上方放置给定参数中描述的内容。这个选项对在已存在的地形上覆盖火把、栅栏/墙壁或道路方块很有用。

## 堆叠复制

命令 `//stack <次数> [方位]` 允许将你选区中的建筑按指定方向排列复制。基本上就是一遍又一遍地将你的选区在同一方向上复制又黏贴。

一些用法：

* 建桥；
* 开掘隧道；
* 重复徒手制作建筑的某一部分。
这里有几个标志可用：

* `-m <蒙版>` 会设置一个源蒙版，使得插件只对符合蒙版的方块进行操作；
* `-e` 会将选区内的实体一并堆叠复制；
* `-b` 会将选区内的生物群系一并堆叠复制（可能需要重进游戏才能见到变动）。

::: info 一些堆叠复制命令的用法  
![img](https://worldedit.enginehub.org/en/latest/_images/stack_bridge.jpg)    
通过 `//stack` 命令延长的桥    
![img](https://worldedit.enginehub.org/en/latest/_images/stack_tunnel.jpg)    
快速地在一座山中挖掘隧道。请在这么做之前确保你已经选中了天花板和地板上的方块。
:::

## 移动

```
//move <距离> [方位] [填充图案]
```
如果你建好了某个东西，却发现它需要移动位置，这个命令就会派上用场，因为它可以帮助你移动建筑物。命令要求一个移动距离的参数，一个可选的方位参数，以及一个同样可选的填充原位置所用方块的参数。

方位参数与 `//stack` 命令使用的相同：使用任意方位，默认为“*me*”——也就是你当前的朝向。

如果你没有提供填充方块的内容，那么移动之后原地不会留下任何方块。

你也可以使用 `-s` 标志来将选区与方块一并移动，这样一来，新的选区仍旧会和方块重合。

移动命令也有与 `//stack` 命令相同的三个标志，`-e` 会将选区内的实体一并移动，`-b` 会将选区内的生物群系一并移动（原本的生物群系不受影响），`-m <蒙版>` 参数则只移动匹配蒙版的方块。

::: info 示例：使用移动命令
将选区向前移动两格方块，原地以石头填充：
```
//move 2 me stone
```
将选区向前移动五格方块，对原地不作处理：
```
//move 5 down
```
:::

## 平滑化

命令 `//smooth [重复次数]` 允许你将地形平滑化。你可以提升平滑操作的次数，让地形变得更加平滑。需要注意的是这个命令依靠高度生效，它只适合平整陆地地形，不适合用于平整洞穴、墙壁或其他物体。

## 重生成

命令 `//regen` 命令允许你将选区内的方块重生成至世界最初的状态。它是基于世界的种子进行地形还原的，因此多次执行命令只会得到完全相同的结果。

::: warning
重生成命令会使用当前世界的生成器，这意味着如果世界是通过外部生成器生成的，或者 Minecraft 的地形生成机制同时被修改，那么重新生成的区域有可能会显得格格不入。
:::

## 自然化

命令 `//naturalize` 会让地形通过覆盖泥土与草方块自然化。
![img](https://worldedit.enginehub.org/en/latest/_images/naturalize.jpg)
这会让地面看起来更加自然。

## 生成植被

命令 `//flora` 将会随机在选区内的草方块上生成高草和花朵，或是在沙地上生成仙人掌和枯死的灌木。该命令的工作机制与覆盖命令相似，但是通过后者做到同一功能显然更加麻烦。

## 植树造林

命令 `//forest <树种> [密度]` 将会根据你的参数生成一片树林。“密度”参数需为处于 0-100 间的整数，它决定了 WorldEdit 在区域内尝试生成树的频率。默认值为 5。

## 掏空区域

命令 `//hollow [厚度] [填充图案]` 将会掏空选区内的结构，使得它们成为厚度一定的空心结构。默认情况下，空心物体的内部是空气，你可以通过命令参数使得结构被掏空后填充指定的方块。

## 绘制曲线

当你创建了一个立方体选区，你可以在你选择的两点间绘制一条线。命令 `//line <图案> [厚度]` 将会以指定的图案和粗细生成一条线段，`-h` 标志将会让绘制的曲线中空，也就是类似于“管道”的形状。

若要绘制一条带有更多点的曲线，通过命令 `//sel convex` 将选择模式切换为多点选择。然后再使用命令 `//curve <图案> [厚度]` 即可按给定图案和粗细绘制出一条经过所有点的曲线（按顺序！），与上一条命令相同，`-h` 参数会让这条曲线中空。

## 设置中心方块

命令 `//center <图案>` 会在选区的中心放置给定的图案，通常为一个方块（或是沿着任意长度轴情况下的两个方块）。

## 选区变换

使用命令 `//deform <表达式>`，你可以对选区内的所有方块施加一个自定义表达式。

表达式需要包含三个参数，即 `x`、`y` 和 `z`，效果是将对应方块复制到它们所对应的新坐标。例如，`y=-1` 会将每个方块向上移动一格，每个额外的方块都会以其原位置的下方方块为准。

默认情况下，坐标会从选择的最小点到最大点为 -1 的位置向 1 的位置延伸。使用 `-r` 标志将会使用原本的世界坐标，而 `-o` 参数则会根据你的放置位置将 Minecraft 的坐标适当偏移。

::: info 示例：变换选区
生成一个丘陵地形：
```
//deform y+=0.2*sin(x*10)
```
将你的选区上下翻转：
```
//deform swap(x,y)
```
:::

## 设置生物群系

WorldEdit 大部分情况下都是用来修改方块的，但是命令 `//setbiome` 允许你设置选区中的生物群系。生物群系的名称需为指定的[群系 ID](https://zh.minecraft.wiki/w/%E7%94%9F%E7%89%A9%E7%BE%A4%E7%B3%BB#%E6%95%B0%E6%8D%AE%E5%80%BC)如果你想要修改为一个模组提供的生物群系，那么 `命名空间:` 前缀就必须添加，例如 `minecraft:plains`。

::: tip
WorldEdit 在其 7.2 之后的版本支持了 Minecraft 于 1.15 版本添加的三维生物群系。这意味着大部分情况下，生物群系只会在你的选区中设置，Y 轴方位上不再会受到影响。不幸的是，Mojang 将三维的生物群系设置为了 4x4x4 的立体空间而不是 1x1x1 的方块，且其边缘也会模糊。这意味着生物群系不能像之前那样被很好的控制，这样它也不能完全贴合你的选区进行设置了。    
额外地，直到 1.18，Mojang 让整个主世界不完全遵守三维生物群系，所以在某些情况下 WorldEdit 将会设置 `Y = 0` 处的生物群系来确保生成和可视化内容。    
若要更进一步地贴合旧版本的行为，请在设置生物群系前先使用命令 `//expand vert`
:::

[^1]: 
    译者注：这条命令的意思是，将选区内用 30% 的羊毛、任意朝向的橡木原木、剪贴板的内容及任意台阶方块填充。  
    带有百分号的为“随机图案”，带有英文星号（*）的为“随机状态图案”，而 #copy 参数则表示剪贴板内容，##slab 则表示任意种类的台阶，包括但不限于橡木台阶、金合欢木台阶、石台阶等。这些内容均在“图案”章节提及，读者可自行翻阅。

[^2]: 译者注：实际上包含了任意与空气接触的沙子，但不会影响处于其下方（默认下方的方块任意一面不与空气接触）的方块。